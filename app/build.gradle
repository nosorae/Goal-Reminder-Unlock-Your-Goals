plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace 'com.yessorae.goalreminder'
    compileSdk 33

    defaultConfig {
        applicationId "com.yessorae.goalreminder"
        minSdk 24
        targetSdk 33
        versionCode 1_1_0_0
        versionName "1.1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue 'string', 'ADMOB_APP_ID', properties["ADMOB_APP_ID"]
            buildConfigField "String", "ADMOB_MAIN_BANNER_ID", properties["ADMOB_MAIN_BANNER_ID"]
            buildConfigField "String", "ADMOB_EDITOR_BANNER_ID", properties["ADMOB_EDITOR_BANNER_ID"]
        }
        debug {
            resValue 'string', 'ADMOB_APP_ID', properties["ADMOB_APP_ID"]
            buildConfigField "String", "ADMOB_MAIN_BANNER_ID", properties["ADMOB_BANNER_TEST"]
            buildConfigField "String", "ADMOB_EDITOR_BANNER_ID", properties["ADMOB_BANNER_TEST"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.6"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    // Allow references to generated code
    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    implementation(project(":designsystem"))
    implementation(project(":presentation"))
    implementation(project(":domain"))
    implementation(project(":di"))
    implementation(project(":common"))

    def composeBom = platform(compose_bom)
    implementation composeBom
    androidTestImplementation composeBom
    // Material Design 3
    implementation 'androidx.compose.material3:material3'
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    // Optional - Add full set of material icons
    implementation 'androidx.compose.material:material-icons-extended'
    // Optional - Add window size utils
    implementation 'androidx.compose.material3:material3-window-size-class'
    // Optional - Integration with activities
    implementation 'androidx.activity:activity-compose:1.6.1'
    // Optional - Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    // compose navigation
    implementation("androidx.navigation:navigation-compose:$nav_version")

    // hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    // firebase
    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // admob
    implementation "com.google.android.gms:play-services-ads:$admob_version"

    // work manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.hilt:hilt-work:$work_hilt_version"
    kapt "androidx.hilt:hilt-compiler:$work_hilt_version"

    // datetime
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:$date_time_version"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_version"


    // delete candidate
    implementation 'androidx.core:core-ktx:1.10.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}


ktlint {
    android.set(true)
    outputColorName.set("RED")
}